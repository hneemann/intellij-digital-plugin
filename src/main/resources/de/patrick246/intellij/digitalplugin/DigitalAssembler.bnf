{
    parserClass="de.patrick246.intellij.digitalplugin.language.parser.DAParser"
    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="DA"
    psiImplClassSuffix="Impl"
    psiPackage="de.patrick246.intellij.digitalplugin.language.psi"
    psiImplPackage="de.patrick246.intellij.digitalplugin.language.psi.impl"

    elementTypeHolderClass="de.patrick246.intellij.digitalplugin.language.psi.DATypes"
    elementTypeClass="de.patrick246.intellij.digitalplugin.language.psi.DAElementType"
    tokenTypeClass="de.patrick246.intellij.digitalplugin.language.psi.DATokenType"
}

daFile ::= line (NEWLINE line)*
line ::=  [label] [('.' directive | instruction)] [commentLine]

instruction ::= (simpleInstructionLine|
                            regRegInstructionLine|
                            regImmInstructionLine|
                            regInstructionLine|
                            immInstructionLine|
                            immRegInstructionLine|
                            indRegRegInstructionLine|
                            regIndRegInstructionLine|
                            indCalcRegInstruction|
                            regIndCalcInstruction|
                            commentLine)
// various different formats for instruction parameters
simpleInstructionLine ::= SIMPLEINSTRUCTION
regRegInstructionLine ::= REGREGINSTRUCTION register COMMA register
regImmInstructionLine ::= REGIMMINSTRUCTION register COMMA expr
regInstructionLine ::= REGINSTRUCTION register
immInstructionLine ::= IMMINSTRUCTION expr
immRegInstructionLine ::= IMMREGINSTRUCTION expr COMMA register
indRegRegInstructionLine ::= INDREGREGINSTRUCTION indRegister COMMA register
regIndRegInstructionLine ::= REGINDREGINSTRUCTION register COMMA indRegister
indCalcRegInstruction ::= INDCALCREGINSTRUCTION indCalcRegister COMMA register
regIndCalcInstruction ::= REGINDCALCINSTRUCTION register COMMA indCalcRegister

// parameter types
register ::= (REGNUMBER | ALIAS)
indRegister ::= "[" register "]"
commentLine ::= COMMENTCHAR COMMENTTEXT
indCalcRegister ::= "[" register "+" expr "]"
expr ::= term '+' expr | term '-' expr | term
term ::= factor '*' term | factor '/' term | factor
factor ::= '(' expr ')' | HEXNUM | ALIAS

// assembler directives
directive ::= (aliasDirectiveLine | aliasRegisterDirectiveLine | stringDirectiveLine | aliasMultivalueDirectiveLine | aliasExprDirectiveLine)
aliasDirectiveLine ::= ALIASDIRECTIVE ALIAS
aliasRegisterDirectiveLine ::= ALIASREGISTERDIRECTIVE registerAliasDefinition REGNUMBER
registerAliasDefinition ::= ALIASDEFINITIONTOKEN
stringDirectiveLine ::=  STRINGDIRECTIVE STRINGTEXT
aliasMultivalueDirectiveLine ::= ALIASMULTIVALUEDIRECTIVE expr (',' expr)*
aliasExprDirectiveLine ::= ALIASEXPRDIRECTIVE immediateAliasDefinition expr
immediateAliasDefinition ::= ALIASDEFINITIONTOKEN

label ::= LABELDEFINITION COLON